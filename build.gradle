plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.13.1'
    id "io.github.goooler.shadow" version '8.1.8'
}

group = 'no.vestlandetmc'
version = '3.2.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:6.3.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.3'
    implementation 'org.bstats:bstats-bukkit:3.1.0'

}
shadowJar {
    relocate 'com.zaxxer.hikari', 'no.vestlandetmc.limbo.libs.hikari'
    relocate 'org.mariadb.jdbc', 'no.vestlandetmc.limbo.libs.mariadb'
    relocate 'org.bstats', 'no.vestlandetmc.limbo.libs.bstats'
    
    archiveFileName.set('Limbo.jar')
}

build.dependsOn shadowJar

jar {
    enabled = false
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}